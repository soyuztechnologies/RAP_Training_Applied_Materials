1. Working with Basic CDS view (Obsolete Concept)

@AbapCatalog.sqlViewName: 'ZATSXXCDSVIEW'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Old approach of CDS views'
define view ZAP_AB_CDS_VIEW as select from zAP_AB_bpa
{
    key bp_id as BpId,
    bp_role as BpRole,
    company_name as CompanyName,
    street as Street,
    country as Country,
    region as Region,
    city as City
}



2. Interface CDS entity on Business partner data


@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Business partners'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZI_AP_AB_BPA as select from zAP_AB_bpa
association[1] to I_Country as _Country on
$projection.CountryCode = _Country.Country
{
    key bp_id as BpId,
    bp_role as BpRole,
    company_name as CompanyName,
    street as Street,
    country as CountryCode,
    region as Region,
    city as City,
    _Country._Text[Language = $session.system_language].CountryName as CountryName
}




3. Interface CDS entity on product data


@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Product master data CDS entity'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZI_AP_AB_PRODUCT as select from zAP_AB_product
{
    key product_id as ProductId,
    name as Name,
    category as Category,
    @Semantics.amount.currencyCode: 'Currency'
    price as Price,
    currency as Currency,
    discount as Discount
}

 
4. Interface CDS entity on sales and items data (concept of association)

@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Sales Fact data'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
define view entity ZI_AP_AB_SALES as select from zAP_AB_so_hdr  as hdr
association[1..*] to zAP_AB_so_item as _Items on
$projection.OrderId = _Items.order_id
{
    key hdr.order_id as OrderId,
    hdr.order_no as OrderNo,
    hdr.buyer as Buyer,
    hdr.created_by as CreatedBy,
    hdr.created_on as CreatedOn,
    _Items    
}


5. Composite CDS entity on Sales and Master data

@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Sales Composite, Cube view for sales data'
@Metadata.ignorePropagatedAnnotations: true
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
@Analytics.dataCategory: #CUBE
define view entity ZI_AP_AB_SALES_CUBE as select from ZI_AP_AB_SALES
association[1] to ZI_AP_AB_BPA as _BusinessPartner on
$projection.Buyer = _BusinessPartner.BpId
association[1] to ZI_AP_AB_PRODUCT as _Product on 
$projection.Product = _Product.ProductId
{
    key ZI_AP_AB_SALES.OrderId,
    key ZI_AP_AB_SALES._Items.item_id as ItemId,
    ZI_AP_AB_SALES.OrderNo,
    ZI_AP_AB_SALES.Buyer,
    ZI_AP_AB_SALES.CreatedBy,
    ZI_AP_AB_SALES.CreatedOn,
    /* Associations */
    ZI_AP_AB_SALES._Items.product as Product,
    @DefaultAggregation: #SUM
    @Semantics.amount.currencyCode: 'CurrencyCode'
    ZI_AP_AB_SALES._Items.amount as GrossAmount,
    ZI_AP_AB_SALES._Items.currency as CurrencyCode,
    @DefaultAggregation: #SUM
    @Semantics.quantity.unitOfMeasure: 'UnitOfMeasure'
    ZI_AP_AB_SALES._Items.qty as Quantity,
    ZI_AP_AB_SALES._Items.uom as UnitOfMeasure,
    _Product,
    _BusinessPartner
}


6. Working with Consumption view with Analytic Query

@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Sales Analytics Consumption view'
@Metadata.ignorePropagatedAnnotations: false
@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
}
@Analytics.query: true
define view entity ZC_AP_AB_SALES_ANA as select from ZI_AP_AB_SALES_CUBE
{
    key _BusinessPartner.CompanyName,
    key _BusinessPartner.CountryName,
    GrossAmount,
    CurrencyCode,
    Quantity,
    UnitOfMeasure
}


